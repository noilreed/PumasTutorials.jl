---
title: Exercise PK03 - Estimation of One Compartment Model with both 1st and 0 order absorption
date: `j Date(now())`
---

```julia; echo = false
using Dates
```

## Objectives

 * For this fitting exercise we wil learn how to fit a first-order absorption model
    and a zero-order absorption model. The first-order model inlcudes a *lag* time.
    We will compare the two models based on the diagnostic plots plots.

The basic workflow for the estimation process is:

 1. Description of the data
 2. Exploratory analysis of the data
 3. NCA Analysis
 4. Pharmacokinetic modelling
 5. Diagnostic Plots
 6. Validation

Lets load the necessary `libraries` before we get started

```julia
using PumasTutorials
using Random
using CSV
using Pumas
using Plots
using StatsPlots
using Pipe
using StatsBase
using PrettyTables
```

## Description of the data

An oral highly polar drug is given to a total of **90 subjects**. Each patient
 receives an oral dose of *20mg* at time=0 and a total of 12 PK samples are collected
 at 0.5, 1, 1.5, 2, 3, 4, 5, 6, 7, 8, 9, 10 hrs.

The following are the units of the dataset:

 * Time (time) = hrs
 * Concentration (dv) = ug/L
 * Dose (amt) = mg

```julia
pk03_data_df = CSV.read("./pk_03.csv", DataFrame, missingstrings = ["", ".", "NA", "BQL"])
```

Basic summary statistics of the data

```julia
stats_pk03_data = describe(pk03_data_df, :min, :max, :mean, :std, :nmissing, cols=[:id,:time,:dv])
```

## Exploratory Plots of the given data

A plot of **Concentration vs Time**

```julia
pk_data_plot = dropmissing(pk03_data_df, :dv)
@df pk_data_plot plot(:time, :dv, group=:id,label=false,
                        xlabel = "Time (hrs)", ylabel="Concentration (ug/L)",
                        xticks = [0,1,2,3,4,5,6,7,8,9,10],
                        size = (600, 600), guidefontsize = 12,
                        title = "Concentration vs Time")
```

## NCA Analysis

We will now perform an NCA analysis to get the initial estimates for the fitting
 of the given data. The route column will need to be included to indicate the dosing
 is extravascular `ev`. The *read_nca* function input requires the route to be
 stated as either ev for extravascular, this will help to compute the parameters
 correctly. The concentrations are in `ug/L`, so we will need to convert the `amt` in *micrograms*
 before we parse the data to the *read_nca* function. We will create a new column called `dose(ug)`
 which we will use for all subsequent analysis.

```julia, results="hidden"
pk03_data_df[!, :dose] = ifelse.(pk03_data_df.amt .== 20, 20000, 0)
pk03_data_df[:, :route] .= "ev"
```

Now, map the data variables to the `read_nca` function that prepares the data for
 NCA analysis. You can even type `?read_nca` in the REPL and get more information on the
 mapping of the

```julia
pk03_nca = read_nca(pk03_data_df,
                    id     = :id,
                    time   = :time,
                    amt    = :dose,
                    conc   = :dv,
                    route  = :route)
```

A full NCAReport is generated, we will then perform summary statistics of the
required parameters to obtain the Mean, GeometricMean, and SD

```julia
pk03_nca_report = NCAReport(pk03_nca, sigdig=3)
```

Perform the `Summary Statistics` for the required NCA Parameters

```julia
## Select the required parameters from the NCA Report
stats_nca_df = select(pk03_nca_report, [:id, :vz_f_obs, :cl_f_obs, :kel, :half_life, :tmax, :tlag])

## Stack the data for easy computation
stats_nca_stacked = stack(stats_nca_df, [:vz_f_obs, :cl_f_obs, :kel, :half_life, :tmax, :tlag,], [:id])
stats_nca_summary = combine(groupby(stats_nca_stacked,[:variable]),
                            [col => fun for col in [:value]
                            for fun in [mean, geomean, std]])
```

We can obtain the Ka value from 0.693/(*tmax*/4) i.e **Ka = 0.7 hr⁻¹**

## Pharmacokinetic Modelling

#### Read the dataset into read_pumas()

First we will try to fit the data to a first-order absorption model. In this case
 the `rate` should be set to `zero`. So we will map the `rate` column to the dataset.

```julia`
## Read dataset to Pumas
pk03_data_first = read_pumas(pk03_data_df,
                             id           = :id,
                             time         = :time,
                             observations = [:dv],
                             amt          = :dose,
                             evid         = :evid,
                             cmt          = :cmt,
                             rate         = :rate)
```

##### First-order Absorption Model with Lag Time

The @dynamics block is written as analytical solutions, you can also specify them as
 differential equations as shown.

```julia
pk_03_first  = @emmodel begin
  @metadata begin
    desc  = "PK 03 First order"
    timeu = u"hr"
  end

  @param begin
    tvtlag  ~ 1 | LogNormal
  end

  @random begin
    "Absorption Rate Constant (1/hr)"
    Ka ~ 1 | LogNormal
    "Clearance (L/hr)"
    CL ~ 1 | LogNormal
    "Volume (L)"
    Vc ~ 1 | LogNormal
  end

  @pre begin
    lags     = (Depot = tvtlag,)
  end

  @dynamics Depots1Central1
    #Depot'   = -Ka*Depot
    #Central' =  Ka*Depot - (CL/Vc)*Central
  #end

  @post begin
    cp       =  Central/Vc
  end

  @error begin
    "Plasma Concentrations (ug/L)"
    dv       ~ ProportionalNormal(cp)
  end
end
```

The initial parameters are obtained from NCA analysis.

```julia
pk03_param_first = ( Ka      = 0.7,
                     CL      = 48,
                     Vc      = 98,
                     tvtlag  = 0.2)
```

Before we start with fitting the data, we will simulate the data with the initial
 estimates of the parameters we have obtained from the NCA analysis. This will help
 us to evaluate the appropiatness of the model.

```julia
sim    = simobs(pk_03_first, pk03_data_first, init_param(pk_03_first))
sim_df = DataFrame(sim)

@df sim_df plot(:time, :dv, group=:id,
                     xlabel = "Time (hrs)", ylabel = "Concentration (ug/L)",
                     title = "Concentration vs Time",
                     size = (600, 600), guidefontsize = 12,
                     label = "", alpha=0.2)
@df pk03_data_df scatter!(:time, :dv, alpha=0.5,
                           label = "Observed concentrations", legend=false)
```

The first-order absorption model does not seem like a good fit for the data. We
 will still use it for comparison with the zero-order model

##### SAEM Analysis


```julia
pk03_fit_first_saem  = @time fit(pk_03_first, pk03_data_first, pk03_param_first,
                                 Pumas.SAEM(), ensemlblealg=EnsembleThreads())

coeftable(pk03_fit_first_saem)
```

##### Zero-order Absorption Model

For the zero-order model we have to set the `rate = -2` and then read the dataset
 again for the zer-order model.

```julia
pk03_data_df[!, :rate_zero] = ifelse.(pk03_data_df.time .== 0, -2, 0)

## Read datset to Pumas
pk03_data_zero = read_pumas(pk03_data_df,
                             id           = :id,
                             time         = :time,
                             observations = [:dv],
                             amt          = :dose,
                             evid         = :evid,
                             cmt          = :cmt,
                             rate         = :rate_zero)
```

```julia
pk_03_zero   = @emmodel begin
  @metadata begin
    desc  = "PK 03 Zero order"
    timeu = u"hr"
  end

  @param begin
    tvTabs ~ 1 | LogNormal
  end

  @random begin
    "Clearance (L/hr)"
    CL ~ 1 | LogNormal
    "Volume (L)"
    Vc ~ 1 | LogNormal
  end

  @pre begin
    duration = (Central = tvTabs,)
  end

  @dynamics Central1
    #Central' =  - (Cl/Vc)*Central
  #end

  @post begin
    cp       = Central/Vc
  end

  @error begin
    dv       ~ ProportionalNormal(cp)
  end
end
```

We will set the parameters for tvcl and tvvc from the final estimates of the first-order
 SAEM fit. The tvTabs can be set to an initial value of 3.5 from the plot of
 concentration vs time. We can see that the `tmax` is around 4.

```julia
pk03_param_zero = ( CL      = 48,
                    Vc      = 101,
                    tvTabs  = 4)
```

We will simulate the zero-order model and plot the observed data on it to see
 the fit of the model.

```julia
sim    = simobs(pk_03_zero, pk03_data_zero, pk03_param_zero)
sim_df = DataFrame(sim)

@df sim_df plot(:time, :dv, group=:id,
                     xlabel = "Time (hrs)", ylabel = "Concentration (ug/L)",
                     title = "Concentration vs Time",
                     size = (600, 600), guidefontsize = 12,
                     label = "", alpha=0.2)
@df pk03_data_df scatter!(:time, :dv, alpha=0.5,
                           label = "Observed concentrations", legend=false)
```

We have dropped the `eta` on tvTabs since it was not estimated with good precision.

```julia
pk03_fit_zero_saem = @time fit(pk_03_zero, pk03_data_zero, pk03_param_zero,
                                 Pumas.SAEM(), ensemlblealg=EnsembleThreads())

coeftable(pk03_fit_zero_saem)
```

Lets compare the estimates for the two models first-oder and zero-order.

```julia
@pipe outerjoin(coeftable(pk03_fit_zero_saem),
                coeftable(pk03_fit_first_saem), on = :parameter, makeunique = true) |>
 rename!(_, :estimate => :pk03_zero_order, :estimate_1 => :pk03_first_order)
```
