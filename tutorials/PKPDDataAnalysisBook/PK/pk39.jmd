---
title: Exercise PK39 - Two Compartment data-Experimental design issues
date: `j Date(now())`
---

```julia; echo = false
using Dates
```

### Background

  * Structural model        - Two compartment linear elimination with zero order absorption
  * Route of administration - Three consecutive constant rate IV infusion
  * Dosage Regimen          - 1st dose:- 26.9 mcg/kg over 15min, 2nd dose:- 139 mcg/kg from 15min to 8hr, 3rd dose:- 138.95 mcg/kg between 8hr to 24hr
  * Number of Subjects      - 1

![pk39](https://user-images.githubusercontent.com/62236414/81842108-197dc900-9569-11ea-8f79-d6a7216a3ad5.png)


### Objectives

In this model you will learn how to build a two compartment model and simulate for a single subject.

### Libraries

Call the "necessary" libraries to get start.

```julia
using Random
using Pumas
using PumasUtilities
using CairoMakie
```

### Model

In this two compartment model we administer three consecutive IV infusion for a
 single subject and we assess the disposition of drug and fitting the model to the
 observed data.

```julia
pk_39           = @model begin
  @metadata begin
    desc        = "Two Compartment Model"
    timeu       = u"hr"
  end

  @param begin
    "Clearance (L/kg/hr)"
    tvcl        ∈ RealDomain(lower=0)
    "Volume of Central Compartment (L/kg)"
    tvvc        ∈ RealDomain(lower=0)
    "Volume of Peripheral Compartment (L/kg)"
    tvvp        ∈ RealDomain(lower=0)
    "Intercompartmental clearance (L/kg/hr)"
    tvq         ∈ RealDomain(lower=0)
    Ω           ∈ PDiagDomain(4)
    "Proportional RUV"
    σ²_prop     ∈ RealDomain(lower=0)
  end

  @random begin
    η           ~ MvNormal(Ω)
  end

  @pre begin
    CL          = tvcl * exp(η[1])
    Vc          = tvvc * exp(η[2])
    Vp          = tvvp * exp(η[3])
    Q           = tvq * exp(η[4])
  end

  @dynamics begin
    Central'    =  (Q/Vp)*Peripheral - (Q/Vc)*Central -(CL/Vc)*Central
    Peripheral' = -(Q/Vp)*Peripheral + (Q/Vc)*Central
  end

  @derived begin
    cp          = @. Central/Vc
    """
    Observed Concentration (ug/L)
    """
    dv          ~ @. Normal(cp, sqrt(cp^2*σ²_prop))
  end
end
```

### Parameters

Parameters provided for simulation. `tv` represents the typical value for parameters.

  * ``Cl`` - Clearance (L/kg/hr)
  * ``Vc`` - Volume of Central Compartment (L/kg)
  * ``Vp`` - Volume of Peripheral Compartment (L/kg)
  * ``Q`` - Intercompartmental clearance (L/kg/hr)
  * ``Ω`` - Between Subject Variability
  * ``σ`` - Residual error

```julia
param = (tvcl    = 0.417793,
         tvvc    = 0.320672,
         tvvp    = 2.12265,
         tvq     = 0.903188,
         Ω       = Diagonal([0.0,0.0,0.0,0.0]),
         σ²_prop = 0.005)
```

### Dosage Regimen

Single subject receiving three consecutive IV infusion
  * 1st dose: **26.9 mcg/kg over 15min**
  * 2nd dose: **139 mcg/kg from 15min to 8hr**
  * 3rd dose: **138.95 mcg/kg between 8hr to 24hr**

```julia
ev1  = DosageRegimen([26.9,139,138.95], time=[0,0.25,8], cmt=1, duration=[0.25,7.85,16])
sub1 = Subject(id=1, events=ev1)
```

### Simulation

Lets simulate for plasma concentration with the specific observation time points after IV infusion.

```julia; results="hidden"
Random.seed!(123)
sim_sub1 = simobs(pk_39, sub1, param, obstimes=0:0.01:60)
```

### Visualization

```julia

f1, a1, p1 = sim_plot(pk_39, [sim_sub1], 
        observations = :cp, 
        color = :redsblues,
        linewidth = 4,
        axis = (
                xlabel = "Time (hr)", 
                ylabel = "PK39 Concentrations (μg/L)",
                xticks = 0:10:60))
f1                
```

```julia; echo=false, eval=false
par = (tvcl    = 0.417793,
       tvvc    = 0.320672,
       tvvp    = 2.12265,
       tvq     = 0.903188,
       Ω       = Diagonal([0.0123,0.0625,0.0154,0.0198]),
       σ²_prop = 0.005)

ev1  = DosageRegimen(26.9, time=0, cmt=1, duration=0.25)
ev2  = DosageRegimen(139, time=0.25, cmt=1, duration=7.85)
ev3  = DosageRegimen(138.95, time=8, cmt=1, duration=16)
evs  = DosageRegimen(ev1,ev2,ev3)
pop  = map(i -> Subject(id=i, events=evs), 1:72)

Random.seed!(1234)
sim_pop = simobs(pk_39, pop, par, obstimes=[0.25,0.5,1,2,3,6,8,9,10,12,18,21,24,24.5,25,26,28,30,32,34,36,42,48,60])
plot(sim_pop)

df_sim = DataFrame(sim_pop)

CSV.write("./data/generated/pk_39.csv", df_sim)
```
